#!/usr/bin/env fish

# Search for an existing tmux session and if doesn't exist, create it
# Based on the awesome script from @ThePrimeagen
# https://github.com/ThePrimeagen/.dotfiles/blob/master/bin/.local/scripts/tmux-sessionizer
# Converted to fish script by @caarlos0
# https://github.com/caarlos0/dotfiles.fish/blob/main/bin/tmux-sessionizer

function search
    find "$DOTFILES" "$CODE_DIR" -maxdepth 1 -type d |
        while read p
            zoxide query -l -s "$p"
            zoxide query -l -s "$p/" # Needed for Code directories
        end |
        grep / | # only show directories
        sort -rnk1 | # sort by score
        uniq | # remove any duplicates
        # awk '{print $2}' | # only show the path
        sed 's|^/Users/Oli|~|' | # replace /Users/Oli prefix with ~
        fzf --height=100% --no-border --no-sort --no-info
end

if test (count $argv) -eq 1
    set selected "$argv[1]"
else
    set selected $(search)
end

if test -z $selected
    exit 0
end

# Add the selected directory to Zoxide's database
zoxide add "$selected"

source ~/.config/fish/config.fish
set selected_name (basename "$selected" | tr . _)
set tmux_running (pgrep tmux)

if test -z "$TMUX" && test -z "$tmux_running"
    set -l selected_abs_path (echo "$selected" | sed "s|^~|$HOME|")
    tmux new-session -s "$selected_name" -c "$selected_abs_path"
    exit 0
end

if ! tmux has-session -t="$selected_name" 2>/dev/null
    set -l selected_abs_path (echo "$selected" | sed "s|^~|$HOME|")
    tmux new-session -ds "$selected_name" -c "$selected_abs_path"
end

tmux switch-client -t "$selected_name"
